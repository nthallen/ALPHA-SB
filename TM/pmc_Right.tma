Partition

State PMC_Right_Init {
   +5 Validate PMC_Right_Stop;
}

State PMC_Right_Stop {
  # If bridge is enabled and in velocity mode and not at zero velocity goto spin down
   +1 { if ((!(PMC_Right_Status6 & 0x10)) && (PMC_Right_Status0 & 0x01) &&
            (!(PMC_Right_Status4 & 0x01))) {
          Validate PMC_Right_Spin_Down;
        }
      }
   +1 Validate PMC_Right_Idle;
}

State PMC_Right_Spin_Down {
      > PMC Right Velocity Set 0 RPM
      Hold until (PMC_Right_Status4 & 0x01) or 10;
   +1 Validate PMC_Right_Idle;
}

State PMC_Right_Ready {
  depending on (PMC_Stop) {
    Validate PMC_Right_Stop;
  }
  depending on (PMC_Lock) {
    Validate PMC_Right_Stop;
  }
  depending on (PMC_Idle) {
    Validate PMC_Right_Idle;
  }
}

State PMC_Right_Idle {
  > PMC Right Brake Apply
  > PMC Right Bridge Disable
  depending on (PMC_Activate) {
    Validate PMC_Right_Activate;
  }
  depending on (PMC_Lock) {
    Validate PMC_Right_Lock;
  }
}

State PMC_Right_Activate {
      > PMC Right Bridge Disable
   +1 > PMC Right Select Velocity Control
   +1 > PMC Right Velocity Set 0 RPM
      Hold until (!(PMC_Right_Status0&1) && (!(PMC_Right_Status6 & 0x10)) && (PMC_Right_RPM_SP == 0)) or 3
      else {
        msg(2, "PMC_Right bridge disable & Vel config & RPM_SP == 0 not observed");
        Validate PMC_Right_Activate;
      }
      
   +1 > PMC Right Bridge Enable
   +1 > PMC Right Brake Release
      Hold until ((PMC_Right_Status0&1) && (PMC_Right_Status6 & 0x20)) or 3
      else {
        msg(2, "PMC_Right bridge disable & Brake release not observed");
        Validate PMC_Right_Activate;
      }

   +1 Validate PMC_Right_Ready;
}

State PMC_Right_Lock {
  # If bridge is enabled and in velocity mode and not at zero velocity goto spin down
   +1 { if ((!(PMC_Right_Status6 & 0x10)) && (PMC_Right_Status0 & 0x01) &&
            (!(PMC_Right_Status4 & 0x01))) {
          Validate PMC_Right_Spin_Down;
        }
      }
   +1 > PMC Right Bridge Disable
      Hold until (!(PMC_Right_Status0&1)) or 3
      else {
        msg(2, "PMC_Right Bridge disable not observed");
        Validate PMC_Right_Lock; }

   +1 > PMC Right Select Position Control
      Hold until (!(PMC_Right_Status0&1) && (PMC_Right_Status6 & 0x10)) or 3
      else {
        msg(2, "PMC_Right Bridge disable && Position Config not observed");
        Validate PMC_Right_Lock; }

   +1 > PMC Right Bridge Enable
      Hold until (PMC_Right_Status0&1) or 3
      else {
        msg(2, "PMC_Right Bridge enable not observed");
        Validate PMC_Right_Lock; }

   +1 > PMC Right Brake Release
      > PMC Right Home Execute Enable
      Hold until (PMC_Right_Status4 & 0x1000) or 3
      else Validate PMC_Right_Lock;
      Hold until (PMC_Right_Status4 & 0x4000) or 10; # Homing Complete
   +1 > PMC Right Home Execute Disable
      Hold until (!(PMC_Right_Status4 & 0x1000)) or 3
      else {
        msg(2, "PMC Right Home Active not observed");
        Validate PMC_Right_Lock; }

   +1 > PMC Right Move to 1700
      Hold until ((PMC_Right_Status4 & 0x0001) == 0x1) or 5; # Zero Vel & At?
   +1 > PMC Right Brake Apply
      > PMC Right Bridge Disable
      Validate PMC_Right_Locked;
}

State PMC_Right_Locked {
  depending on (PMC_Activate) {
    Validate PMC_Right_Activate;
  }
}
