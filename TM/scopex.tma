  State Init {
  > Telemetry Start
}

State Shutdown {
}

PARTITION

%{
  bool AscenderPwrRequest = false;
%}

# State AscPwr_Disable {}

State AscPwr_Init {
  # Check whether ascender is powered or not
  # If any loads are on and/or any loads read close to BusV,
  # all loads should read close to BusV
   +5 { if (convert(AscPri1_V) > 77.0) {
          AscenderPwrRequest = true;
          Validate AscPwr_Seq0;
        } else Validate AscPwr_Off;
      }
}

State AscPwr_Off {
  depending on (1 Hz) { if (AscenderPwrRequest) Validate AscPwr_Seq0; }
}

State AscPwr_Seq0 {
      > Power Ascender Precharge N2 On
      Hold Until (convert(AscPri1_V) > 77.0) or 3
      else Validate AscPwr_Seq0_Fail;
   +1 > Power Ascender Precharge N3 On
      > Power Ascender Secondary N1 On
      > Power Ascender Secondary N4 On
  +10 { if (convert(AscPri1_V) < 77.0 || !AscenderPwrRequest) {
          AscenderPwrRequest = false;
          Validate AscPwr_Shutdown;
        }
      }
}

State AscPwr_Seq0_Fail {
  > Power Ascender All Off
}

State AscPwr_Shutdown {
  > Power Ascender All Off
  Validate AscPwr_Off;
}

